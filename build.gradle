ext {
    configuration = [
            compileVersion   : 25,
            buildToolsVersion: "25.0.2",
            minSdk           : 9,
            targetSdk        : 25,
            exoplayerVersion : "r1.5.13"
    ]
    bintrayConfig = [
            userOrg    : "blipinsk",
            userName   : "Bartosz Lipinski",
            userEmail  : "bartoszlipinski90@gmail.com",
            groupId    : "com.bartoszlipinski",
            name       : "ExoPlayer1",
            artifact   : "exoplayer1",
            description: "External packagings of 1.x.x versions of Google's ExoPlayer",
            website    : "https://github.com/blipinsk/ExoPlayer1",
            gitUrl     : "https://github.com/blipinsk/ExoPlayer1.git",
            licenses   : ["Apache-2.0"],
            licenseDesc: "The Apache Software License, Version 2.0"
    ]

    bintrayUser = BINTRAY_USERNAME
    bintrayKey = BINTRAY_API_KEY
    dryRun = 'false'
    isUploading = project.getGradle().startParameter.taskNames.any { it.contains('bintrayUpload') }
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.novoda:bintray-release:0.3.4'
        // clean build bintrayUpload
    }
}

subprojects {
    repositories {
        jcenter()
    }

    group = bintrayConfig.groupId
    version = configuration.exoplayerVersion

    if (isUploading && project.name in [bintrayConfig.artifact]) {
        println project.name
        apply plugin: 'maven'

        gradle.taskGraph.whenReady { taskGraph ->
            taskGraph.getAllTasks().find {
                it.path == ":$project.name:generatePomFileForMavenPublication"
            }.doLast {
                file("build/publications/maven/pom-default.xml").delete()
                pom {
                    //noinspection GroovyAssignabilityCheck
                    project {
                        name project.name
                        artifactId project.name
                        packaging 'aar'
                        description bintrayConfig.description
                        url bintrayConfig.website
                        version configuration.exoplayerVersion

                        scm {
                            url bintrayConfig.gitUrl
                            connection bintrayConfig.gitUrl
                            developerConnection bintrayConfig.gitUrl
                        }

                        licenses {
                            license {
                                name bintrayConfig.licenseDesc
                            }
                        }

                        developers {
                            developer {
                                id bintrayConfig.userOrg
                                name bintrayConfig.userName
                                email bintrayConfig.userEmail
                            }
                        }
                    }
                }.writeTo("build/publications/maven/pom-default.xml")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
